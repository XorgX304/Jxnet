
allprojects {

    apply plugin: 'java'

    group = 'com.ardikars.jxnet'
    version = '1.1.1-SHAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    javadoc {
        def currentYear = "2017"
        options.with {
            encoding 'UTF-8'
            charSet 'UTF-8'
            author true
            windowTitle "Jxnet ${project.version}"
            header "Jxnet ${project.version}"
            docTitle "Jxnet ${project.version} API Documentation"
            footer 'https://github.com/ardikars/Jxnet'
            bottom "Copyright &copy: 2015 - ${currentYear} Jxnet. All rights reserved."
        }
    }

}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }
jar.dependsOn subprojects.tasks["classes"]

jar {
    subprojects.each { subproject ->
        from subproject.sourceSets.main.output.classesDir
        from subproject.sourceSets.main.output.resourcesDir
    }
}

javadoc {
    def srcDirs = []
    subprojects.each { project ->
        srcDirs.addAll project.sourceSets.main.java.srcDirs
    }
    source srcDirs
}

