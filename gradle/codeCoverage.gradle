
/**
 * Copyright (C) 2015-2018 Jxnet
 */

subprojects {

    if (!project.name.equals("jxnet-native")) {

        apply plugin: 'jacoco'
        apply plugin: 'com.github.kt3k.coveralls'

        jacoco {
            toolVersion = "${JACOCO_VERSION}"
        }

        jacocoTestReport {
            reports {
                csv.enabled false
                xml.enabled true
                html.enabled = true
                html.destination file("${buildDir}/jacocoHtml")
            }
        }

        test {
            testLogging {
                showStandardStreams = true
            }
            jacoco {
                append = false
                destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
                classDumpDir = file("$buildDir/jacoco/classpathdumps")
            }
        }

        /**
         * Upload converage report
         */
        tasks.coveralls {
            dependsOn 'check'
        }

    }

}

jacocoTestReport {
    def reportingProjs = subprojects.findAll { it.hasProperty 'jacocoTestReport' }
    def sourceDirList = []
    def classDirList = []
    def execDataList = []
    reportingProjs.each {
        dependsOn it.test
        sourceDirList.add files(it.sourceSets.main.java.srcDirs)
        classDirList.add it.sourceSets.main.output
        execDataList.add it.jacocoTestReport.executionData
    }
    sourceDirectories = files sourceDirList
    classDirectories = files classDirList
    executionData = files execDataList
}
